<!DOCTYPE HTML> 
<html lang="en"> 
<head>
<title>Hello Terrain </title>
<meta charset="utf-8"> 
</head>

    
<script id="shader-vs" type="x-shader/x-vertex">
    attribute vec3 aVertexNormal;
    attribute vec3 aVertexPosition;
    
    uniform mat4 uMVMatrix;
    uniform mat4 uPMatrix;
    uniform mat3 uNMatrix;

    uniform vec3 uKDiffuse;
    
    varying vec3 vPosition;
    varying vec3 vNormal;
    varying vec3 vKDiffuse;
  
    void main(void) {
    
    // Get the vertex position in eye coordinates
    vec4 vertexPositionEye4 = uMVMatrix * vec4(aVertexPosition, 1.0);
    vPosition = vertexPositionEye4.xyz / vertexPositionEye4.w;
    // Transform the normal (n) to eye coordinates
    vNormal = normalize(uNMatrix * aVertexNormal);

    // Different color depending on height
    float height = aVertexPosition.z;
    if (height > 0.05){
        vKDiffuse = vec3( 221.0/255.0, 221.0/255.0, 221.0/255.0);   // Grey
    } else if (height > 0.0){
        vKDiffuse = vec3(221.0/255.0, 181.0/255.0, 105.0/255.0);    // Brown
    } else if (height > -0.06){
        vKDiffuse = vec3(125.0/255.0, 216.0/255.0, 125.0/255.0);    // Green
    } else{
        vKDiffuse = vec3(34.0/255.0, 184.0/255.0, 221.0/255.0);     // Blue 
    }

    // Set edges color
    if( uKDiffuse == vec3(0.0,0.0,0.0) || uKDiffuse == vec3(1.0,1.0,1.0)  ){
        vKDiffuse = uKDiffuse;
    }
    
    gl_Position = uPMatrix*uMVMatrix*vec4(aVertexPosition, 1.0);
         
    }
</script>
  
<script id="shader-fs" type="x-shader/x-fragment">
    precision mediump float;
    varying vec3 vPosition;
    varying vec3 vNormal;
    varying vec3 vKDiffuse;
  
    uniform float uShininess;
    uniform vec3 uLightPosition;
    uniform vec3 uAmbientLightColor;
    uniform vec3 uDiffuseLightColor;
    uniform vec3 uSpecularLightColor;

    uniform vec3 uKAmbient;
    uniform vec3 uKSpecular;

    uniform float uIsFogOpen;

    const float LOG2 = 1.442695;
  
  
    void main(void) {
  
    // Calculate the vector (l) to the light source
    vec3 vectorToLightSource = normalize(uLightPosition - vPosition);
  
    // Calculate n dot l for diffuse lighting
    float diffuseLightWeightning = max(dot(vNormal, vectorToLightSource), 0.0);
                                      
    // Calculate the reflection vector (r) that is needed for specular light
    vec3 reflectionVector = normalize(reflect(-vectorToLightSource, vNormal));
    
    // The camera in eye coordinates is located in the origin and is pointing
    // along the negative z-axis. Calculate viewVector (v) 
    // in eye coordinates as:
    // (0.0, 0.0, 0.0) - vPosition
    vec3 viewVectorEye = -normalize(vPosition);

    // Calculate the halfway vector (H) that is needed for specular light
    vec3 halfwayVector = normalize(vectorToLightSource + viewVectorEye);
    
    // float rdotv = max(dot(reflectionVector, viewVectorEye), 0.0);
    float ndoth = max(dot(vNormal, halfwayVector), 0.0);
    
    float specularLightWeightning = pow(ndoth, uShininess);
  
  
    // Sum up all three reflection components and send to the fragment shader
        vec4 fragColor = vec4(((uAmbientLightColor*uKAmbient)
          + (uDiffuseLightColor*vKDiffuse) * diffuseLightWeightning
          + (uSpecularLightColor*uKSpecular) * specularLightWeightning),1.0);

    // excute the fog shader
    float fogCoord = (gl_FragCoord.z/gl_FragCoord.w);
    vec4  fogColor = vec4(1.0, 1.0, 1.0, 1.0);
    float fogDensity = 0.0;
    if (uIsFogOpen == 1.0) {
        fogDensity = 0.7;
    }
    float fogFactor = exp2( -fogDensity * fogDensity * fogCoord * fogCoord * LOG2);

    // fog
    fogFactor = clamp(fogFactor, 0.0, 1.0);
    gl_FragColor = mix(fogColor, fragColor, fogFactor);

    }
</script>

<script src="gl-matrix-min.js"></script> 
<script src="webgl-utils.js"></script> 
<script src="Terrain.js"></script>  
<script src="mp2.js"></script>   
<body onload="startup();">
    <div style="color: white; position: absolute;">
            <p>**********  User Interface  **********</p>
            <p>**** Left Arrow ← ：Roll Left </p>
            <p>**** Right Arrow → ：Roll Right </p>
            <p>**** Up Arrow ↑ ：Pitch Up </p>
            <p>**** Down Arrow ↓ ：Pitch Down </p>
            <p>**** Keyborad - a ：Yaw Left </p>
            <p>**** Keyborad - d : Yaw Right </p>
            <p>**** Plus + / Equal = ：Increase Speed </p>
            <p>**** Minue - ：Decrease Speed </p>
            <p>**************************************</p>
    </div>
    <canvas id="myGLCanvas" width="800" height="800"></canvas>
    <form id="fog_control">
        <fieldset>
            <legend>Fog Control</legend>
            <input type="radio" name="primitive" id="fogOpen" value="fogOpen" checked> Fog Open
            <input type="radio" name="primitive" id="fogClose" value="fogClose" > Fog Close
        </fieldset>
        </form>
    <form id="input_form">
    <fieldset>
        <legend>Rendering Parameters</legend>
        <input type="radio" name="primitive" id="wireframe" value="wireframe"> Wireframe
        <input type="radio" name="primitive" id="polygon" value="polygon" > Polygon
        <input type="radio" name="primitive" id="wirepoly" value="wirepoly" checked> Polygon with Edges  
    </fieldset>
    </form>
</body>

</html>
